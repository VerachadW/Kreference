apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco'

ext {
    publish_version = '0.7.0'
}

android {
    compileSdkVersion 25
    buildToolsVersion "$build_tools_version"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    jacoco {
        version "0.7.1.201405082137"
    }
}

jacoco {
    toolVersion "0.7.1.201405082137"
}

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled = true
        html.enabled = true
    }
    classDirectories = fileTree(
            dir: "$buildDir/intermediates/classes/debug",
            excludes: ['**/R*.class',
                       '**/*$InjectAdapter.class',
                       '**/*$ModuleAdapter.class',
                       '**/*$ViewInjector*.class'
            ])
    onlyIf = {
        true
    }
    sourceDirectories = files('src/main/kotlin')
    executionData = files("$buildDir/jacoco/testDebugUnitTest.exec")
    // Bit hacky but fixes https://code.google.com/p/android/issues/detail?id=69174.
    // We iterate through the compiled .class tree and rename $$ to $.
    doFirst {
        new File("$buildDir/intermediates/classes/").eachFileRecurse { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$android_lib_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile "junit:junit:$junit_version"
    testCompile("org.robolectric:robolectric:$robolectric_version"){
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

apply plugin: 'com.novoda.bintray-release'

publish {
    userOrg = 'verachadw'
    groupId = 'com.github.verachadw.kreference'
    artifactId = 'kreference'
    bintrayUser = "$System.env.BINTRAY_USER"
    bintrayKey = "$System.env.BINTRAY_API_KEY"
    publishVersion = "$publish_version"
    licences = ['MIT']
    desc = 'Preference as Properties for Kotlin Android'
    website = 'https://github.com/VerachadW/Kreference'
}

